# Find libdwarf.
# Requires:
#   LibElf
# Will set:
#   LIBDWARF_FOUND
#   LIBDWARF_INCLUDE_DIRS
#   LIBDWARF_LIBRARIES
#   LIBDWARF_DEFINITIONS

find_package(LibElf)

if(LIBDWARF_FOUND)
	return()
endif()

# Find dwarf.h

if(not DWARF_H_INCLUDE_DIR)
	find_path(DWARF_H_INCLUDE_DIR "dwarf.h")
	if(DWARF_H_INCLUDE_DIR)
		set(DWARF_H_DEFINITIONS -DHAVE_DWARF_H)
	endif()
endif()

if(not DWARF_H_INCLUDE_DIR)
	find_path(DWARF_H_INCLUDE_DIR "libdwarf/dwarf.h")
	if(DWARF_H_INCLUDE_DIR)
		set(DWARF_H_DEFINITIONS -DHAVE_LIBDWARF_DWARF_H)
	endif()
endif()

# Find libdwarf.h

if(not LIBDWARF_H_INCLUDE_DIR)
	find_path(LIBDWARF_H_INCLUDE_DIR "libdwarf.h")
	if(LIBDWARF_H_INCLUDE_DIR)
		set(LIBDWARF_H_DEFINITIONS -DHAVE_LIBDWARF_H)
	endif()
endif()

if(not LIBDWARF_H_INCLUDE_DIR)
	find_path(LIBDWARF_H_INCLUDE_DIR "libdwarf/libdwarf.h")
	if(LIBDWARF_H_INCLUDE_DIR)
		set(LIBDWARF_H_DEFINITIONS -DHAVE_LIBDWARF_LIBDWARF_H)
	endif()
endif()

# Find library

find_library(LIBDWARF_LIBRARY dwarf)

# Handle the arguments

include(FindPackageHandleStandardArgs)

find_package_handle_standard_args(LibDwarf
	DEFAULT_MSG
	LIBDWARF_LIBRARY
	DWARF_H_INCLUDE_DIR
	LIBDWARF_H_INCLUDE_DIR
	LIBELF_FOUND
)

if(LIBDWARF_FOUND)
	set(LIBDWARF_INCLUDE_DIRS
		${DWARF_H_INCLUDE_DIR}
		${LIBDWARF_H_INCLUDE_DIR}
		${LIBELF_INCLUDE_DIRS}
	)
	set(LIBDWARF_LIBRARIES
		${LIBDWARF_LIBRARY}
		${LIBELF_LIBRARIES}
	)
	set(LIBDWARF_DEFINITIONS
		${DWARF_H_DEFINITIONS}
		${LIBDWARF_H_DEFINITIONS}
		${LIBELF_DEFINITIONS}
	)
else(LIBDWARF_FOUND)
	set(LIBDWARF_INCLUDE_DIRS NOTFOUND)
	set(LIBDWARF_LIBRARIES NOTFOUND)
	set(LIBDWARF_DEFINITIONS NOTFOUND)
endif(LIBDWARF_FOUND)
